apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        applicationId "org.totschnig.myexpenses"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode 257
        versionName '2.7.1'

        buildConfigField "String", "PLATTFORM", "\"Android\""
        buildConfigField "String", "BUILD_DATE", "\"" + getDate() + "\""
        testInstrumentationRunner "org.totschnig.myexpenses.MyTestRunner"
        testProguardFile "proguard-test.txt"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            //signingConfig signingConfigs.release
            manifestPlaceholders = [appName: "@string/app_name"]
        }
        debug {
            applicationIdSuffix ".debug"
            manifestPlaceholders = [appName: "My Expenses Debug"]
        }
    }

    lintOptions {
        disable 'InvalidPackage', 'MissingQuantity', 'MissingTranslation', 'ImpliedQuantity'
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "2048M"
        jumboMode = true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    testBuildType "release"
    useLibrary 'org.apache.http.legacy'
}

dependencies {
    apt 'com.google.dagger:dagger-compiler:' + project.daggerVersion
    compile 'com.google.dagger:dagger:' + project.daggerVersion
    compile project(':StickyListHeaders:library')
    compile project(':MaterialSeekBarPreference:library')
    compile 'com.android.support:appcompat-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:support-v4:' + project.androidSupportLibraryVersion
    compile 'com.android.support:recyclerview-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:cardview-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:design:' + project.androidSupportLibraryVersion
    compile 'com.android.support:gridlayout-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:preference-v14:' + "23.1.1"
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    //noinspection GradleDynamicVersion
    androidTestCompile 'com.android.support.test.espresso:espresso-core:' + project.espressoVersion
    androidTestCompile 'com.android.support:support-annotations:' + project.androidSupportLibraryVersion
    androidTestCompile 'com.android.support.test:runner:' + project.androidSupportTestVersion
    androidTestCompile 'com.android.support.test:rules:' + project.androidSupportTestVersion
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:' + project.espressoVersion
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:' + project.espressoVersion) {
        exclude module: 'accessibility-test-framework'
    }

    compile 'com.android.support:multidex:1.0.0'

    compile 'frankiesardo:icepick:3.2.0'
    compile 'com.itextpdf:itextpdf:5.5.8'
    compile 'ch.acra:acra:4.9.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-csv:1.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.google.code.gson:gson:2.6.1'
    compile 'com.google.firebase:firebase-ads:9.8.0'
    compile 'com.google.android.gms:play-services-maps:9.8.0'
    compile 'com.github.PhilJay:MPAndroidChart:bd75e6c'
    compile 'com.github.mtotschnig:Caldroid:master-SNAPSHOT'
    compile 'com.commonsware.cwac:wakeful:1.0.+'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.google.android.gms:play-services:9.8.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    testCompile 'junit:junit:4.12'
    androidTestCompile 'junit:junit:4.12'
    apt 'frankiesardo:icepick-processor:3.2.0'
    provided 'javax.annotation:jsr250-api:1.0'
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd-HHmm')
    return formattedDate
}

//https://code.google.com/p/android/issues/detail?id=187730
//can be removed after migration to Android Gradle Plugin 2
configurations.compile.dependencies.each { compileDependency ->
    println "Excluding compile dependency: ${compileDependency.getName()}"
    configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
        configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
    }
}

